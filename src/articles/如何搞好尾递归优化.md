---
uuid: "229a679c-b172-4d4f-8f58-0474bc9f6259"
title: "如何搞好尾递归优化"
english: "Tail-recursive-done-right"
date: 2023-08-18
category: "学习"
outdated: false
draft: false
ref: 
  - name: "Tail Call"
    url: "https://en.wikipedia.org/wiki/Tail_call"
  - name: "Ocaml Tail Recursion"
    url: "https://cs3110.github.io/textbook/chapters/basics/functions.html#tail-recursion"
  - name: "LAMBDA: The Ultimate GOTO"
    url: "https://dl.acm.org/doi/pdf/10.1145/800179.810196"
  - name: "Exploring JS - Tail call optimization"
    url: "https://exploringjs.com/es6/ch_tail-calls.html"
---

## 概念

首先要区分一下「尾递归」和「尾调用」，前者是一种特殊的递归优化方式，后者是一种通用的概念。所谓「尾调用」，就是指一个函数的最后一个动作是返回一个函数的调用结果的情形，其最后一步新调用的返回值直接被当前函数的返回结果。而尾调用优化是一种编译器优化技术。尾调用优化的工作原理是，在函数的尾部调用自身时，编译器会将该调用转换为循环，从而**避免了创建新的栈帧**。

```ocaml
let fun_a a = a * a;;

(* 属于尾调用 *)
let fun_b b = fun_a b;;

(* 不属于尾调用 *)
let fun_c c = 1 + fun_a c;;
```

很多时候，**递归**是一个解决问题的常见做法，递归函数不断调用自身，直到达到某个终止条件。例如下面的 `sum` 函数：

```ocaml
let rec sum = function
  | [] -> 0
  | h :: t -> h + sum t;;
```

对于比较小的列表，这个函数运行的很好，但是如果列表很长，就会爆栈[^n1]。

[^n1]:
  考虑一下这个有趣的 C 程序 ：
  `int main(){return main();}`，
  它会怎么样？你可以在文章末尾看看答案。

```ocaml
sum (List.init 1000000 (fun (x) -> x));;
(* Stack overflow during evaluation (looping recursion?). *)
```

当函数调用时，需要在内存中保存一些信息，这些信息一层一层叠加起来，也就是函数的调用记录。大多数语言都使用栈实现函数调用，当一个函数体的计算调用另一个函数时，一个新元素被压入调用栈，并在被调用函数完成时弹出。栈的大小受到操作系统的限制，如果空间不足，就会导致栈溢出。分析一下上面的 `sum` 函数，可以发现，每当进行一次递归，都要「展开」一次 `h + sum t`，于是内存中的情况是这样的：

```lisp
(h1 + 
  (h2 +
    (h3 + ......)))
```

结果是在**最后一次递归调用，也就是整个式子完全展开**以后执行的 $h_n+h_{n-1}+h_{n-2}+...+h_1$，而在此之前所有的调用记录，即每一次的 $h_n$ 都被保留在栈中，当中间的数足够多自然栈空间会不足。而使用「尾调用」就能够避免这个结果：尾调用不需要保存中间信息，所以从头到尾只需要一个调用栈即可：

```ocaml
let rec better_sum list =  
  let rec _sum acc = function
    | [] -> acc
    | h :: t -> _sum (acc + h) t in _sum 0 list;;
```

## 如何改写

把一个给定的递归函数改写成尾递归优化形式是有规律可循的：

1. 将原本的递归函数改写，增加一个额外的参数，一般记作 `acc` 即 `accumulator`；

2. 写一个包装函数用来调用改写后的函数，并将累加器的初始值传入。写包装函数的原因很简单，你也不想用户调用函数需要填一个莫名其妙的「累加器初始值」吧；

3. 更改辅助函数以返回基本情况下的累加器；

4. 优化辅助函数的递归情况。在递归调用之前对累加器参数执行额外的工作。这是唯一需要脑力的一步。

以阶乘函数[^note2]为例，其原本的函数（没有优化）如下：

[^note2]:
  这个版本的阶乘函数对于很大的数回返回 $0$，原因是整数类型溢出。

```ocaml
let rec fact n = if n = 0 then 1 else n * fact (n - 1)
```

首先是第一步，把原本的函数改写，添加一个新的参数 `acc`:

```ocaml
let rec fact_aux n acc = if n = 0 then 1 else n * fact_aux (n - 1)
```

接下来，写一个新的包装函数（其中 $1$ 是 `acc` 的初始值），这个包装函数可以不是递归的：

```ocaml
let fact n = fact_aux n 1
```

然后，更改改写后的原函数，一般来讲这一步就是把原本的终止条件改成 `acc`：

```ocaml
let rec fact_aux n acc = if n = 0 then acc else n * fact_aux (n - 1)
```

最后对 `fact_aux` 进行一点修改，得到：

```ocaml
let rec fact_aux n acc =
  if n = 0 then acc else fact_aux (n - 1) (n * acc)

let fact n = fact_aux n 1

(* 也可以这样写，避免暴露 [fact_aux] 函数 *)
let fact n =
  let rec fact_aux n acc = 
    if n = 0 then acc else fact_aux (n - 1) (n * acc)
  in fact_aux n 1
```

## 实例

### 列表的长度

```ocaml
let rec length  = function
  | [] -> 0
  | h :: t -> 1 + length t;;
```

按照之前的方法，首先添加累加器参数 `acc`，再包装整个函数到新的函数中：

```ocaml
let length list = (* 外层函数 *)
  let rec aux result = function (* 内层函数 *)
    | []      -> result (* 列表没东西了，返回结果 *)
    | h :: t  -> aux (result + 1) t (* 否则，拆成头和尾巴，加一递归 *)
  in aux 0 list (* 调用内层函数，初始值 0 *) ;;
```

### map 函数

标准的 `map` 函数（目前标准库中的实现）不是尾递归优化的版本，其实现如下：

```ocaml
let rec map f = function
  | [] -> []
  | a :: l -> let r = f a in r :: map f l
```

其中 `let` 是为了防止副作用。现在改写成优化的版本：首先加一个 `acc`，把终止情况也改成返回 `acc`，接着改写内部函数，先将 $f$ 应用于拆开的列表第一个元素，再将其他传入 `map`:

```ocaml
let map f list =
  let rec aux f acc = function
    | [] -> List.rev acc
    | a :: l -> aux f (f a :: acc) l
  in aux f [] list;;
```

### filter 函数

```ocaml
let rec filter p = function
  | [] -> []
  | h :: t -> if p h then h :: filter p t else filter p t
```

对它进行尾递归优化：

```ocaml
let rec filter_aux p acc = function
  | [] -> List.rev acc
  | h :: t -> if p h then filter_aux p (h :: acc) t else filter_aux p acc t
```

### 斐波那契数列

这是一个使用递归解决问题的经典问题，虽然并不一定高效：

```ocaml
let rec fib = function
  | 0 -> 0
  | 1 -> 1
  | n -> fib (n - 1) + fib (n - 2);;
```

请注意：这已经是一个尾调用的版本，但「包装函数，累加器」的思想可以推广到这里：

```ocaml
let fib_fast = function
  | 0 -> None
  | n -> 
    let rec aux n pp p =
      match n with
      | 1 -> Some p
      | n -> aux (n - 1) p (pp + p)
    in aux n 0 1;;
```

## 其他语言中的实现

### JavaScript

ES6 规定所有 ECMAScript 的实现，都必须部署尾调用优化。

```js
function factorial(n, total) {
  if (n === 1) return total;
  return factorial(n - 1, n * total);
}
```

### C

还是以上面那个注释里的程序为例：

```c
int main(){
  return main();
}
```

事实上，运行结果的答案不是唯一的。考虑到不同的编译器版本和编译器优化参数，这个程序的表现可能有两种：由于爆栈导致 `segfault` 退出，或者陷入僵死状态（死循环）。

当使用 `gcc` 版本 13.2.1，不使用任何优化（`gcc -Og main.c`）时，程序会爆栈，其反汇编如下：

```lisp
00000000004011ef <main>:
  4011ef:       48 83 ec 08             sub    $0x8,%rsp <- stack pointer
  4011f3:       b8 00 00 00 00          mov    $0x0,%eax
  4011f8:       e8 f2 ff ff ff          call   4011ef <main>
  4011fd:       48 83 c4 08             add    $0x8,%rsp
  401201:       c3                      ret
```

很明显，每一次调用都会将栈指针寄存器 `%rsp` 移动，使得栈不断向下增长，这种情况下会不断调用 `main` 函数直到爆栈。而若是启用 `-O3` 优化（`gcc -O3 main.c`）时，程序会陷入僵死状态，反汇编如下：

```lisp
0000000000401080 <main>:
  401080:       eb fe                   jmp    401080 <main>
  401082:       66 2e 0f 1f 84 00 00    cs nopw 0x0(%rax,%rax,1)
  401089:       00 00 00 
  40108c:       0f 1f 40 00             nopl   0x0(%rax)
  401090:       90                      nop
  401091:       90                      nop
  401092:       90                      nop
  401093:       90                      nop
......
```

可以看见调用被优化掉了，除了一些对齐指令啥也没有了。另外 `clang` 编译器（版本 16.0.6）无论是否开启优化，均会生成死循环版本。可见：[尾递归优化不仅仅取决于程序本身，也取决于编译器能不能正确的识别和优化]{.underline}。幸运的是，大多数语言的主流编译器都支持这样的优化。

最后来一个阶乘的例子：

```c
long factorial(long n, long total){
    if (n == 1) return total;
    return factorial(n - 1,n * total);
}
```
